/* Part 2: Parser */

/**
 *  Package and Import Specifications
 */
import java_cup.runtime.*;

/**
 *  Usercode Components
 */
parser code {:
    // Connect this parser to a scanner!
    Scanner s;
    Parser(Scanner s){ this.s=s; }
:}

/* define how to connect to the scanner! */
scan with {: return s.next_token(); :};

/**
 *  Symbol Lists
 */

/* Terminals (tokens returned by the scanner). */
terminal               IF, ELSE, PREFIX, SUFFIX, PLUS, COMMA, RPAREN, LCURLY, RCURLY;
terminal String        IDENTIFIER, FUNC, STRING_LITERAL;

/*  Non terminals */
non terminal           program;
non terminal String    def_list, def, def_args, def_expr, def_cond, expr_list, expr, cond, call, call_args, deactivate;

/**
 *  Precedence Declarations
 */
//TODO: Maybe precedence solves S/R
precedence left FUNC;
precedence right IF, ELSE;
precedence left PLUS;
precedence left PREFIX, SUFFIX;

/**
 *  The Grammar Rules
 */



program       ::= def_list:d deactivate:e              {:System.out.println("public class Main {");
                                                               System.out.println("\tpublic static void main(String[] args) {");
                                                               System.out.printf("%s", e);
                                                               System.out.println("\t}");
                                                               System.out.printf("%s", d);
                                                                    System.out.println("}");
                                                                          :}
                    ;

deactivate ::= {: RESULT = ""; :} ;

/* First Part: Function Declarations */
def_list      ::= def_list:rest def:f                                {: RESULT = String.format("%s\n%s\n", rest, f); :}
                |                                                    {: RESULT = ""; :}
                ;
def           ::= FUNC:f def_args:a RPAREN LCURLY def_expr:e RCURLY  {: RESULT = String.format("\tpublic static String %s%s) {\n\t\treturn %s;\n\t}", f, a, e); :}
                ;
def_args      ::= def_args:rest COMMA IDENTIFIER:i                   {: RESULT = String.format("%s, String %s", rest, i); :}
                | IDENTIFIER:i                                       {: RESULT = String.format("String %s", i); :}
                |                                                    {: RESULT = ""; :}
                ;
// Identifiers only allowed in function definitions... Otherwise expr, cond would be enough
def_expr      ::= call:f                                             {: RESULT = f; :}
                | IF def_cond:c RPAREN def_expr:e1 ELSE def_expr:e2  {: RESULT = String.format("%s ? %s : %s", c, e1, e2); :}
                | def_expr:e1 PLUS def_expr:e2                       {: RESULT = String.format("%s + %s", e1, e2); :}
                | IDENTIFIER:i                                       {: RESULT = i; :}
                | STRING_LITERAL:s                                   {: RESULT = String.format("\"%s\"", s); :}
                ;
def_cond      ::= def_expr:e1 PREFIX def_expr:e2                     {: RESULT = String.format("(%s).startsWith(%s)", e1, e2); :}
                | def_expr:e1 SUFFIX def_expr:e2                     {: RESULT = String.format("(%s).endsWith(%s)", e1, e2); :}
                ;

/* Second Part: List of expressions to print (they all are/return Strings) */
expr_list     ::= expr_list:rest expr:f                              {: RESULT = String.format("%s\t\tSystem.out.println(%s);\n", rest, f); :}
                |                                                    {: RESULT = ""; :}
                ;
//TODO parentheses for complex expr?
//TODO fix if (if)
expr          ::= call:f                                             {: RESULT = f; :}
                | IF cond:c RPAREN expr:e1 ELSE expr:e2              {: RESULT = String.format("%s ? %s : %s", c, e1, e2); :}
                | expr:e1 PLUS expr:e2                               {: RESULT = String.format("%s + %s", e1, e2); :}
                | STRING_LITERAL:s                                   {: RESULT = String.format("\"%s\"", s); :}
                ;
call          ::= FUNC:f call_args:a RPAREN                          {: RESULT = String.format("%s%s)", f, a); :}
                ;
call_args     ::= call_args:rest COMMA expr:e                        {: RESULT = String.format("%s, %s", rest, e); :}
                | expr:e                                             {: RESULT = e; :}
                |                                                    {: RESULT = ""; :}
                ;
cond          ::= expr:e1 PREFIX expr:e2                             {: RESULT = String.format("(%s).startsWith(%s)", e2, e1); :}
                | expr:e1 SUFFIX expr:e2                             {: RESULT = String.format("(%s).endsWith(%s)", e2, e1); :}
                ;
